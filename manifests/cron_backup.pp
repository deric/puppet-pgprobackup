# @api private
# A cron job is exported from a database instance, but could be executed elsewhere.
# Typically on a catalog (backup) server.
define pgprobackup::cron_backup (
  String                          $id,
  String                          $cluster,
  String                          $host_group,
  Pgprobackup::Backup_type        $backup_type,
  String                          $server_address,
  String                          $db_name,
  String                          $db_user,
  String                          $version,
  String                          $backup_user,
  String                          $remote_user,
  Integer                         $remote_port,
  Stdlib::AbsolutePath            $backup_dir,
  Boolean                         $redirect_console,
  Boolean                         $delete_expired,
  Boolean                         $merge_expired,
  Boolean                         $temp_slot,
  Boolean                         $validate,
  Boolean                         $archive_wal,
  Integer                         $compress_level,
  Pgprobackup::Hour               $hour = 4,
  Pgprobackup::Minute             $minute = 0,
  Pgprobackup::Month              $month = '*',
  Pgprobackup::Weekday            $weekday = '*',
  Optional[Pgprobackup::Monthday] $monthday = undef,
  Optional[Integer]               $archive_timeout = undef,
  Optional[Stdlib::AbsolutePath]  $log_dir = undef,
  Optional[Pgprobackup::LogLevel] $log_level_file = undef,
  Optional[Pgprobackup::LogLevel] $log_level_console = undef,
  Optional[String]                $log_file = undef,
  Optional[Integer]               $retention_redundancy = undef,
  Optional[Integer]               $retention_window = undef,
  Optional[Integer]               $threads = undef,
  Optional[String]                $slot = undef,
  Optional[String]                $compress_algorithm = undef,
  Optional[String]                $binary = undef,
  Optional[String]                $log_console = undef,
  Optional[String]                $log_rotation_size    = undef,
  Optional[String]                $log_rotation_age     = undef,
) {
  @@cron { "pgprobackup_${backup_type}_${server_address}-${host_group}":
    command  => epp('pgprobackup/cron_backup.epp', {
        id                   => $id,
        cluster              => $cluster,
        db_name              => $db_name,
        db_user              => $db_user,
        version              => $version,
        host_group           => $host_group,
        backup_dir           => $backup_dir,
        backup_type          => $backup_type,
        backup_user          => $backup_user,
        server_address       => $server_address,
        delete_expired       => $delete_expired,
        retention_redundancy => $retention_redundancy,
        retention_window     => $retention_window,
        merge_expired        => $merge_expired,
        threads              => $threads,
        temp_slot            => $temp_slot,
        slot                 => $slot,
        validate             => $validate,
        compress_algorithm   => $compress_algorithm,
        compress_level       => $compress_level,
        archive_timeout      => $archive_timeout,
        archive_wal          => $archive_wal,
        log_dir              => $log_dir,
        log_file             => $log_file,
        log_level_file       => $log_level_file,
        log_level_console    => $log_level_console,
        remote_user          => $remote_user,
        remote_port          => $remote_port,
        binary               => $binary,
        redirect_console     => $redirect_console,
        log_console          => $log_console,
        log_rotation_size    => $log_rotation_size,
        log_rotation_age     => $log_rotation_age,
    }),
    user     => $backup_user,
    weekday  => $weekday,
    hour     => $hour,
    minute   => $minute,
    month    => $month,
    monthday => $monthday,
    tag      => "pgprobackup-${host_group}",
  }
}
